plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}
group = 'com.pygstocks'
version = 'Ver-25.12'
repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}
dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}
tasks.named('runServer') {
    it.minecraftVersion('1.21.10')
}
tasks.named('shadowJar') {
    archiveClassifier.set('all')
    zip64 true
}
tasks.named('jar') {
    enabled = false
}
tasks.build.dependsOn tasks.shadowJar
processResources {
    inputs.property 'version', version
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(version: version)
    }
}